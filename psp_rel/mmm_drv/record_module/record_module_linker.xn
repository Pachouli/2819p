/**************************************************************************
*                                                                         *
*   PROJECT     : MIPS port for UCOS-II                                   *
*                                                                         *
*   MODULE      : bank a&b link.xn                                        *
*                                                                         *
*   AUTHOR      : wuyueqian                                               *
*                                                                         *
*   PROCESSOR   : MIPS 4KEc (32 bit RISC) - Actions board                 *
*                                                                         *
*   Tool-chain  : Cygnus                                                  *
*                                                                         *
*                                                                         *
*   Linker script file                                                    *
*                                                                         *
**************************************************************************/

/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

/*ap 代码和数据段链接地址*/
RCODE_TEXT_ADDR = RECORD_MODULE_VA_START;


RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_RECORD_MODULE_DATA_ADDR;
SRAM_TEXT_ADDR = RCODE_ADDR_BASE + 0x0; /*暂时不需要sram_tex段，需要时先在link_base.xn分配段空间*/


DATA_SIZE_RESERVE_4BYTES = SRAM_RECORD_MODULE_DATA_SIZE - 4;



OUTPUT_ARCH(mips)

ENTRY(record_module_cmd_entry)

SECTIONS
{
    /*常驻代码*/
    .sram_text SRAM_TEXT_ADDR	:
    {
    	. = 0x0; /*暂时不需要sram_tex段，需要时先在link_base.xn分配段空间*/
    }

    .entry RECORD_MODULE_ENTRY_VA_START :
    {
    	*/obj/record_module_cmd.o(.rodata.entry)
    }
    .text RCODE_TEXT_ADDR	: 
    {
        */obj/*.o(.text* .rodata*)
    }

    /* 初始化了的全局数据段*/
    .data RDATA_DATA_ADDR   :  
    {
        _rd_data_start = ABSOLUTE(.); /*NOTE!!不能去掉这一行，否则如果.data段为空，可能会导致.bss段连接地址错误*/
        */obj/*.o(.data) 
    }
    .bss :
    {
		*/obj/*.o(.bss COMMON)
	
    	. = DATA_SIZE_RESERVE_4BYTES - SIZEOF(.data);
    }
}

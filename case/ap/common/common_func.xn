
/*定义GROUP BANK INDEX 和其他地址变量*/
INPUT(link_base.xn)

RCODE_TEXT_ADDR = DRV_APCOMMON_VA_START;
RDATA_DATA_ADDR = RDATA_ADDR_BASE + SRAM_AP_LIB_DATA_ADDR;
SRAM_TEXT_ADDR = RCODE_ADDR_BASE + 0x0; /*暂时不需要sram_tex段，需要时先在link_base.xn分配段空间*/

SRAM_AP_LIB_DATA_SIZE_RESERVE_4BYTES = SRAM_AP_LIB_DATA_SIZE - 4;
RDATA_RANDOM_LAP_BUFFER_ADDR = RDATA_ADDR_BASE + SRAM_RANDOM_LAP_BUFFER_ADDR;
RDATA_PRINT_DMA_CACHE_ADDR = RDATA_ADDR_BASE + SRAM_PRINT_DMA_CACHE_ADDR;
RDATA_VRAM_MERGE_TEMP_BUFFER_ADDR = RDATA_ADDR_BASE + SRAM_VRAM_MERGE_TEMP_BUFFER_ADDR;

RDATA_BLE_SPP_RECV_CACHE1_ADDR = RDATA_ADDR_BASE + SRAM_BLE_SPP_RECV_CACHE1_ADDR;
RDATA_BLE_SPP_RECV_CACHE2_ADDR = RDATA_ADDR_BASE + SRAM_BLE_SPP_RECV_CACHE2_ADDR;

RCODE_SRAM_TEXT1_ADDR = RCODE_ADDR_BASE + SRAM_AP_LIB_SRAM_TEXT_1_ADDR;

OUTPUT_ARCH(mips)

ENTRY(common_op)

SECTIONS
{
    .sram_text SRAM_TEXT_ADDR	:
    {
    	. = 0x0; /*暂时不需要sram_tex段，需要时先在link_base.xn分配段空间*/
    }

    .sram_text1 RCODE_SRAM_TEXT1_ADDR	:
    {
        ./obj/common_standby_s2.o(.TEXT.S2 .BSS.S2)
    }

    /*etnry table偏移需要与sdk_link_base.xn, api.h保持一致*/
    .entry RCODE_TEXT_ADDR :
    {
        ./obj/common_op_entry.o(.rodata)
    }
    
    .text	:
    {
        KEEP(./obj/*.o(.my_entry))
        ./obj/*.o(.text .text.* .rodata .rodata.*)
        enhanced.a(.text .text.* .rodata .rodata.*)
    }

    BSS_RANDOM_LAP_BUFFER RDATA_RANDOM_LAP_BUFFER_ADDR	:
    {
    	./obj/common_bt_load.o(.bss.random_lap_buffer)
    }

    BSS_PRINT_DMA_CACHE RDATA_PRINT_DMA_CACHE_ADDR	:
    {
    	./obj/common_init.o(.bss.print_dma_cache)
    }

    BSS_VRAM_MERGE_TEMP_BUFFER RDATA_VRAM_MERGE_TEMP_BUFFER_ADDR	:
    {
    	./obj/common_config.o(.bss.vram_merge)
    }

	BSS_BLESPP_RECV_CACHE1 RDATA_BLE_SPP_RECV_CACHE1_ADDR	:
	{
		./obj/common_blespp_data_trans.o(.bss_blespp_recv_cache1)
	}
	BSS_BLESPP_RECV_CACHE2 RDATA_BLE_SPP_RECV_CACHE2_ADDR	:
	{
		./obj/common_blespp_data_trans.o(.bss_blespp_recv_cache2)
	}

    /* 初始化了的全局数据段，紧跟在 .text段后面: .data */
    .data RDATA_DATA_ADDR   : 
    {        
        _rd_data_start = ABSOLUTE(.); /*NOTE!!不能去掉这一行，否则如果.data段为空，可能会导致.bss段连接地址错误*/
        ./obj/*.o(.data)
    }
    /* 未初始化的全局数据段，由系统初始为0 */
    .bss    :
    {
        ./obj/*.o(.bss)
        enhanced.a(.bss)
        . = SRAM_AP_LIB_DATA_SIZE_RESERVE_4BYTES - SIZEOF(.data); /*防止.data段不是4字节对齐*/
    }
}
